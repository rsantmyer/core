CREATE OR REPLACE PACKAGE PKG_APPLICATION
AS
--Usage:
-- This package is the core application registry and deployment-control API.
-- It records which database applications are installed, which version is currently
-- deployed, what other applications they depend on, which privileges they require,
-- which database objects they own, and which metadata rows they own inside tables
-- registered by other applications.
--
-- Typical deployment lifecycle:
--   1. Start or resume a deployment with begin_deployment_p.
--   2. Register dependencies, required privileges, owned objects, and owned metadata.
--   3. Validate dependencies, privileges, and objects.
--   4. Mark the deployment complete, or failed, before the script exits.
--
-- For an initial deployment using the default version 0.1.0:
--   EXEC pkg_application.begin_deployment_p(ip_application_name => ':APPLICATION_NAME:');
--
-- For an initial deployment using an explicit semantic version:
--   EXEC pkg_application.begin_deployment_p
--   ( ip_application_name => ':APPLICATION_NAME:'
--   , ip_major_version    => 1
--   , ip_minor_version    => 0
--   , ip_patch_version    => 0
--   , ip_deployment_type  => pkg_application.c_deploy_type_initial
--   );
--
-- Register dependencies and requirements:
--   EXEC pkg_application.add_dependency_p(ip_application_name => ':APPLICATION_NAME:', ip_depends_on => ':APPLICATION2_NAME:');
--   EXEC pkg_application.add_sys_priv_p  (ip_application_name => ':APPLICATION_NAME:', ip_privilege => ':SYS_PRIV:');
--   EXEC pkg_application.add_obj_priv_p
--   ( ip_application_name => ':APPLICATION_NAME:'
--   , ip_owner            => ':OBJECT_OWNER:'
--   , ip_type             => ':OBJECT_TYPE:'
--   , ip_name             => ':OBJECT_NAME:'
--   , ip_privilege        => ':OBJECT_PRIV:'
--   );
--
-- Validate and finish:
--   EXEC pkg_application.validate_dependencies_p  (ip_application_name => ':APPLICATION_NAME:');
--   EXEC pkg_application.validate_obj_privs_p     (ip_application_name => ':APPLICATION_NAME:');
--   EXEC pkg_application.validate_sys_privs_p     (ip_application_name => ':APPLICATION_NAME:');
--   EXEC pkg_application.validate_objects_p       (ip_application_name => ':APPLICATION_NAME:');
--   EXEC pkg_application.set_deployment_complete_p(ip_application_name => ':APPLICATION_NAME:');
--------------------------------------------------------------------------------
--------------------------------------------------------------------------------
   TYPE t_application IS RECORD
   ( application_name           application.application_name%TYPE
   , major_version              application.major_version%TYPE    := 0
   , minor_version              application.minor_version%TYPE    := 1
   , patch_version              application.patch_version%TYPE    := 0
   , pre_release                application.pre_release%TYPE
   , build                      application.build%TYPE  
   , deploy_type                application.deploy_type%TYPE
   , deploy_status              application.deploy_status%TYPE
   , deploy_commit_hash         application.deploy_commit_hash%TYPE
   , deploy_begin               application.deploy_begin%TYPE
   , deploy_end                 application.deploy_end%TYPE 
   )
   ;
   TYPE t_app_object_metadata IS RECORD
   ( object_name               app_object_metadata.object_name%TYPE
   , object_namespace          app_object_metadata.object_namespace%TYPE
   , object_type               app_object_metadata.object_type%TYPE
   , application_name          app_object_metadata.application_name%TYPE
   , discriminator_col         app_object_metadata.discriminator_col%TYPE
   , discriminator_val         app_object_metadata.discriminator_val%TYPE
   , last_update               app_object_metadata.last_update%TYPE
   , version                   app_object_metadata.version%TYPE
   , dml_override_proc         app_object_metadata.dml_override_proc%TYPE
   )
   ;
--------------------------------------------------------------------------------
   --PACKAGE CONSTANTS
   --
   --APPLICATION.version
   c_core_major_version       CONSTANT APPLICATION.major_version%TYPE     := 2;
   c_version_default_major    CONSTANT APPLICATION.major_version%TYPE     := 0;
   c_version_default_minor    CONSTANT APPLICATION.minor_version%TYPE     := 1;
   c_version_default_patch    CONSTANT APPLICATION.patch_version%TYPE     := 0;
   --APPLICATION.deploy_type
   c_deploy_type_initial      CONSTANT APPLICATION.deploy_type%TYPE := 'I';
   c_deploy_type_major        CONSTANT APPLICATION.deploy_type%TYPE := 'V';
   c_deploy_type_minor        CONSTANT APPLICATION.deploy_type%TYPE := 'M';
   c_deploy_type_patch        CONSTANT APPLICATION.deploy_type%TYPE := 'P';
   --APPLICATION.deploy_commit_hash
   c_deploy_commit_hash_unknown CONSTANT APPLICATION.deploy_commit_hash%TYPE := '0000000000000000000000000000000000000000';
   --APPLICATION.deploy_status
   c_deploy_status_running    CONSTANT APPLICATION.deploy_status%TYPE := 'R';
   c_deploy_status_complete   CONSTANT APPLICATION.deploy_status%TYPE := 'C';
   c_deploy_status_fail       CONSTANT APPLICATION.deploy_status%TYPE := 'F';
   --
   --APP_DEPENDENCY.is_valid
   c_valid_unknown            CONSTANT APP_DEPENDENCY.is_valid%TYPE   := 'U';
   c_valid_yes                CONSTANT APP_DEPENDENCY.is_valid%TYPE   := 'Y';
   c_valid_no                 CONSTANT APP_DEPENDENCY.is_valid%TYPE   := 'N';
   --APP_DEPENDENCY.version_min
   c_version_min_any          CONSTANT APP_DEPENDENCY.version_min%TYPE := 0;
   c_version_max_any          CONSTANT APP_DEPENDENCY.version_max%TYPE := 999999;
   --APP_OBJECT_TYPE.object_type%TYPE
   c_object_type_table        CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'TABLE';
   c_object_type_sequence     CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'SEQUENCE';
   c_object_type_procedure    CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'PROCEDURE';
   c_object_type_function     CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'FUNCTION';
   c_object_type_materialized_view     CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'MATERIALIZED VIEW';
   c_object_type_type         CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'TYPE';
   c_object_type_type_body    CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'TYPE BODY';
   c_object_type_package      CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'PACKAGE';
   c_object_type_package_body CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'PACKAGE BODY';
   c_object_type_view         CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'VIEW';
   c_object_type_db_link      CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'DATABASE LINK';
   c_object_type_synonym      CONSTANT APP_OBJECT_TYPE.object_type%TYPE := 'SYNONYM';
   
   --FUNCTIONS
/**
 * @description Returns the currently registered semantic version for an application
 * in major.minor.patch format.
 * @param ip_application_name Application to inspect.
 * @return Current registered application version as text.
 */
   FUNCTION get_current_version_f( ip_application_name IN application.application_name%TYPE )
      RETURN VARCHAR;

/**
 * @description Converts a major.minor.patch version string into a sortable integer
 * in MMMMIIIIPPPP format. This allows version ranges to be compared numerically.
 * @param ip_version Semantic version string, such as '1.2.3'.
 * @return Serialized integer representation of the version.
 */
   FUNCTION serialize_version_f(ip_version IN VARCHAR)
      RETURN INTEGER;

/**
 * @description Converts a serialized MMMMIIIIPPPP integer version back into
 * major.minor.patch format.
 * @param ip_serialized_version Serialized integer version.
 * @return Semantic version string.
 */
   FUNCTION deserialize_version_f(ip_serialized_version IN INTEGER)
      RETURN VARCHAR;
   
   --PROCEDURES
/**
 * @description Raises an error unless ip_application_name is deployed at or above
 * the supplied minimum semantic version. Use this at the start of scripts that
 * require a previously installed application version.
 * @param ip_application_name Application whose version should be checked.
 * @param ip_min_major_version Minimum required major version.
 * @param ip_min_minor_version Minimum required minor version.
 * @param ip_min_patch_version Minimum required patch version.
 */
   PROCEDURE check_min_app_version_p( ip_application_name  IN application.application_name%TYPE
                                    , ip_min_major_version IN application.major_version%TYPE DEFAULT 0
                                    , ip_min_minor_version IN application.minor_version%TYPE DEFAULT 0
                                    , ip_min_patch_version IN application.patch_version%TYPE DEFAULT 0
                                    );

/**
 * @description Raises an error when the requested version has already been deployed.
 * Use this to prevent accidental re-execution of non-idempotent deployment scripts.
 * @param ip_application_name Application whose deployed version should be checked.
 * @param ip_min_major_version Version being deployed - major component.
 * @param ip_min_minor_version Version being deployed - minor component.
 * @param ip_min_patch_version Version being deployed - patch component.
 */
   PROCEDURE check_already_deployed_p( ip_application_name  IN application.application_name%TYPE
                                     , ip_min_major_version IN application.major_version%TYPE DEFAULT 0
                                     , ip_min_minor_version IN application.minor_version%TYPE DEFAULT 0
                                     , ip_min_patch_version IN application.patch_version%TYPE DEFAULT 0 );

/**
 * @description Starts a deployment run for ip_application_name. The procedure records
 * the target semantic version, deployment type, commit hash, start timestamp, and
 * running status. This is normally the first call in an application deployment script.
 * @param ip_application_name Application being deployed.
 * @param ip_major_version Target major version.
 * @param ip_minor_version Target minor version.
 * @param ip_patch_version Target patch version.
 * @param ip_deployment_type Initial, major, minor, or patch deployment type constant.
 * @param ip_deploy_commit_hash Source-control commit hash for traceability.
 * @param ip_redeploy_okay TRUE allows re-running an already deployed version.
 */
   PROCEDURE begin_deployment_p( ip_application_name   IN application.application_name%TYPE
                               , ip_major_version      IN application.major_version%TYPE
                               , ip_minor_version      IN application.minor_version%TYPE
                               , ip_patch_version      IN application.patch_version%TYPE
                               , ip_deployment_type    IN application.deploy_type%TYPE DEFAULT c_deploy_type_initial
                               , ip_deploy_commit_hash IN application.deploy_commit_hash%TYPE DEFAULT c_deploy_commit_hash_unknown
                               , ip_redeploy_okay      IN BOOLEAN DEFAULT FALSE
                               );
/**
 * @description Stores free-form deployment notes for the current deployment.
 * @param ip_application_name Application whose deployment notes should be updated.
 * @param ip_notes Notes to record.
 */
   PROCEDURE set_deploy_notes_p( ip_application_name IN application.application_name%TYPE
                               , ip_notes            IN app_deploy_notes.notes%TYPE);
/**
 * @description Marks the active deployment complete and records its end timestamp.
 * @param ip_application_name Application whose deployment should be marked complete.
 */
   PROCEDURE set_deployment_complete_p( ip_application_name IN application.application_name%TYPE);
/**
 * @description Marks the active deployment failed and records its end timestamp.
 * @param ip_application_name Application whose deployment should be marked failed.
 */
   PROCEDURE set_deployment_fail_p( ip_application_name IN application.application_name%TYPE);
/**
 * @description Registers that ip_application_name depends on another application,
 * optionally constrained to a serialized minimum and maximum version range.
 * @param ip_application_name Application declaring the dependency.
 * @param ip_depends_on Required application.
 * @param ip_version_min Minimum serialized version allowed.
 * @param ip_version_max Maximum serialized version allowed.
 */
   PROCEDURE add_dependency_p( ip_application_name IN application.application_name%TYPE
                             , ip_depends_on       IN app_dependency.depends_on%TYPE
                             , ip_version_min      IN app_dependency.version_min%TYPE DEFAULT c_version_min_any
                             , ip_version_max      IN app_dependency.version_max%TYPE DEFAULT c_version_max_any
                             );
/**
 * @description Validates all registered dependencies for an application and records
 * whether each dependency is currently satisfied.
 * @param ip_application_name Application whose dependencies should be validated.
 */
   PROCEDURE validate_dependencies_p( ip_application_name IN application.application_name%TYPE);
/**
 * @description Registers a required system privilege for an application.
 * @param ip_application_name Application requiring the privilege.
 * @param ip_privilege System privilege name.
 */
   PROCEDURE add_sys_priv_p( ip_application_name IN application.application_name%TYPE
                           , ip_privilege        IN app_sys_privs.privilege%TYPE);
/**
 * @description Validates registered system privileges for an application against
 * privileges currently available to the executing schema.
 * @param ip_application_name Application whose system privileges should be validated.
 */
   PROCEDURE validate_sys_privs_p( ip_application_name IN application.application_name%TYPE);
/**
 * @description Registers a required object privilege for an application.
 * @param ip_application_name Application requiring the privilege.
 * @param ip_owner Owner of the referenced object.
 * @param ip_type Object type.
 * @param ip_name Object name.
 * @param ip_privilege Required object privilege.
 */
   PROCEDURE add_obj_priv_p( ip_application_name IN application.application_name%TYPE
                           , ip_owner            IN app_obj_privs.owner%TYPE
                           , ip_type             IN app_obj_privs.type%TYPE
                           , ip_name             IN app_obj_privs.name%TYPE
                           , ip_privilege        IN app_obj_privs.privilege%TYPE);
/**
 * @description Validates registered object privileges for an application against
 * privileges currently available to the executing schema.
 * @param ip_application_name Application whose object privileges should be validated.
 */
   PROCEDURE validate_obj_privs_p( ip_application_name IN application.application_name%TYPE);
/**
 * @description Registers a database object as being owned by an application. Registered
 * objects can later be validated and physically dropped by delete_application_p.
 * @param ip_application_name Application that owns the object.
 * @param ip_object_name Object name.
 * @param ip_object_type Object type.
 */
   PROCEDURE add_object_p( ip_application_name IN application.application_name%TYPE
                         , ip_object_name      IN app_objects.object_name%TYPE
                         , ip_object_type      IN app_object_type.object_type%TYPE DEFAULT c_object_type_table
                         );
   --
/**
 * @description Registers metadata rows owned by ip_application_name that reside in a table
 * belonging to another application. On delete_application_p, core will automatically remove
 * these rows via DELETE FROM ip_object_name WHERE ip_discriminator_col = ip_discriminator_val,
 * or by calling ip_dml_override_proc when a plain DELETE is insufficient.
 * @param ip_application_name The application claiming ownership of the metadata rows.
 * @param ip_object_name The table containing the metadata rows. Must already be registered
 * in APP_OBJECTS (under any application).
 * @param ip_object_type Object type of ip_object_name. Currently only TABLE is supported.
 * @param ip_discriminator_col The column name that identifies rows belonging to this application.
 * Use 'NONE' when ip_dml_override_proc handles all identification internally.
 * @param ip_discriminator_val The value in ip_discriminator_col that marks rows as owned by
 * ip_application_name. Use 'NONE' together with ip_dml_override_proc.
 * @param ip_version Optional semantic version (major.minor.patch) of the metadata registration.
 * @param ip_dml_override_proc Optional procedure name called as BEGIN <proc>; END; instead of
 * the default DELETE. Use when cleanup requires dropping dynamic objects or cascading deletes.
 */
   PROCEDURE add_object_metadata_p( ip_application_name  IN application.application_name%TYPE
                                  , ip_object_name       IN app_objects.object_name%TYPE
                                  , ip_object_type       IN app_object_type.object_type%TYPE DEFAULT c_object_type_table
                                  , ip_discriminator_col IN app_object_metadata.discriminator_col%TYPE DEFAULT 'NONE'
                                  , ip_discriminator_val IN app_object_metadata.discriminator_val%TYPE DEFAULT 'NONE'
                                  , ip_version           IN app_object_metadata.version%TYPE DEFAULT NULL
                                  , ip_dml_override_proc IN app_object_metadata.dml_override_proc%TYPE DEFAULT NULL
                                  );
/**
 * @description Removes a single metadata registration and executes its associated cleanup
 * (DELETE or dml_override_proc). Called automatically by delete_application_p for all
 * registered metadata; call directly only to deregister a specific entry mid-deployment.
 * @param ip_application_name The application that owns the metadata registration.
 * @param ip_object_name The table the metadata registration points to.
 * @param ip_object_type Object type of ip_object_name.
 * @param ip_discriminator_col The discriminator column recorded at registration time.
 * @param ip_discriminator_val The discriminator value recorded at registration time.
 */
   --
   PROCEDURE delete_object_metadata_p( ip_application_name  IN application.application_name%TYPE
                                     , ip_object_name       IN app_objects.object_name%TYPE
                                     , ip_object_type       IN app_object_type.object_type%TYPE DEFAULT c_object_type_table
                                     , ip_discriminator_col IN app_object_metadata.discriminator_col%TYPE
                                     , ip_discriminator_val IN app_object_metadata.discriminator_val%TYPE
                                     );
/**
 * @description Validates that all database objects registered to an application
 * currently exist.
 * @param ip_application_name Application whose registered objects should be validated.
 */
   PROCEDURE validate_objects_p( ip_application_name IN application.application_name%TYPE); --bugbug: should we validate object_metadata?
/**
 * @description Fully removes an application and all objects registered to it. Cleanup order:
 * (1) all metadata registrations are processed (dml_override_proc called where set, otherwise
 * DELETE FROM table WHERE col = val); (2) all registered database objects are physically dropped
 * via DROP statements; (3) application and dictionary records are deleted.
 * @param ip_application_name The application to remove.
 * @param ip_fail_on_not_found 'Y' (default) raises an error if the application is not found;
 * 'N' silently succeeds.
 */
   PROCEDURE delete_application_p( ip_application_name  IN application.application_name%TYPE
                                 , ip_fail_on_not_found IN VARCHAR2 DEFAULT 'Y' );
/**
 * @description Deletes all application-registry data and drops all registered objects.
 * This is destructive and is intended for development reset scenarios, not routine
 * production deployments.
 */
   PROCEDURE delete_system_p;
/**
 * @description Physically drops a database object by type and name. The registry row,
 * if any, is not removed by this procedure.
 * @param ip_object_type Object type to drop.
 * @param ip_object_name Object name to drop.
 */
   PROCEDURE drop_object_p( ip_object_type IN app_objects.object_type%TYPE
                          , ip_object_name IN app_objects.object_name%TYPE );

/**
 * @description Removes an object from the registry without physically dropping it.
 * @param ip_object_type Object type to forget.
 * @param ip_object_name Object name to remove from the registry.
 */
   PROCEDURE forget_object_p( ip_object_type IN app_objects.object_type%TYPE
                            , ip_object_name IN app_objects.object_name%TYPE );
/**
 * @description Physically drops an object and removes its registry entry. This is
 * destructive and should be used only when the object is known to be application-owned.
 * @param ip_object_type Object type to drop and forget.
 * @param ip_object_name Object name to drop and remove from the registry.
 */
   PROCEDURE drop_and_forget_object_p( ip_object_type IN app_objects.object_type%TYPE
                                     , ip_object_name IN app_objects.object_name%TYPE );
/**
 * @description Transfers a registered object from one application owner to another.
 * Use this when refactoring application boundaries without physically moving or
 * recreating the database object.
 * @param ip_object_name Object whose registry ownership should change.
 * @param ip_new_application_name New owning application.
 * @param ip_object_type Object type.
 * @param ip_old_application_name Optional current owner used to disambiguate duplicate names.
 */
   PROCEDURE change_object_application_p( ip_object_name           IN app_objects.object_name%TYPE
                                        , ip_new_application_name  IN application.application_name%TYPE
                                        , ip_object_type           IN app_object_type.object_type%TYPE DEFAULT c_object_type_table
                                        , ip_old_application_name  IN application.application_name%TYPE DEFAULT NULL );
   --
END PKG_APPLICATION;
/
